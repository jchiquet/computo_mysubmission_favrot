
    model {

        # Likelyhood #####################################################
        for (i in 1:Q){
            Y[i] ~ dpois(N[i] * lb[i])
            Z[i] ~ dbinom(pi[i], N[i])

            log(lb[i]) = beta0[ID[i]] + gamma0[INSEC[i]] + (alpha1 + 
                         gamma1[INSEC[i]]) * TIME[i] + u[ST[i]] + epsi[i]

            pi[i] = 1 - exp(- lb[i])
            epsi[i] ~ dnorm(0, pi_eps)
        }

        for (j in 1:K){
            beta0[j] ~ dnorm(alpha0, tau0)
        }

        for (c in 1:M){
            u[c] ~ dnorm(0, invchi)
        }

        gamma0[1] = 0
        gamma1[1] = 0

        # Priors #########################################################
        for (s in 2:L){
            gamma0[s] ~ dnorm(0, 0.001)
            gamma1[s] ~ dnorm(0, 0.001)
        }

        alpha0 ~ dnorm(0, 0.001)
        alpha1 ~ dnorm(0, 0.001)
        sigma0 ~ dunif(0, 10)
        chi ~ dunif(0, 10)
        eta ~ dunif(0, 10)

        # Derived Quantities #############################################
        tau0 = pow(sigma0, -2)
        invchi = pow(chi, -2)
        pi_eps = pow(eta, -2)

        for (h in 2:L){
            for(t in 1 : T){
                Eff[h, t]  = (1 - exp(gamma0[h] + gamma1[h] * 
                             TIME_unique[t])) * 100
            }
        }  
    }

